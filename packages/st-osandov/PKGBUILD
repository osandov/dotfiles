# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.8.1.r13.g4f4bccd
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="https://st.suckless.org"
conflicts=('st')
_commit=4f4bccd1627c845330235721f593d2e93418723d
source=("${_pkgname}::git+https://git.suckless.org/st#commit=$_commit"
        config.h
        0001-Whitelist-key-event-modifiers-for-shortcuts.patch
        0002-Use-more-sane-logic-for-word-delimiters.patch)
sha256sums=('SKIP'
            '8a0cb594d0ac7e6b71dbec3fb463e7311c6e1949dc0f952702229e99b9872b99'
            '38543647f7ca9be6a1e63eb81b3f241f2b2f1436ef704413756613906dca0b53'
            'b4a0170895e1a6cb86d380c4aedc8c1d9cf69d2df384d15a7979738b843773a6')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  # skip terminfo which conflicts with ncurses
  sed -i '/^\s*tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
