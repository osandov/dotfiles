# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.6.r56.g610723a
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('x86_64' 'i686')
url="http://st.suckless.org"
license=('MIT')
options=(zipman)
depends=('libxft' 'libxext')
makedepends=('git' 'ncurses')
conflicts=('st')
_commit=610723a58f1fbd3c5d911c853a8b94c315fc7c2a
source=("${_pkgname}::git+http://git.suckless.org/st#commit=$_commit"
        config.h)
md5sums=('SKIP'
         '50095d364a51d4eb3cdddde798fedc2f')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
