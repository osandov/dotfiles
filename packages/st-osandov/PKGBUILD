# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.7.r20.ge7ed326
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
conflicts=('st')
_commit=e7ed326d2e914a57017c9f34459824614075519b
source=("${_pkgname}::git+http://git.suckless.org/st#commit=$_commit"
        config.h
        0001-Use-more-sane-logic-for-word-delimiters.patch)
sha256sums=('SKIP'
            '94fe34e1a358f2371ee64ae1df35e2bf72dbaafada0b12ff6c7175882ff8e70d'
            '0fb284ad68855b30a8d6e47d7d69bf708df00739284384307db920d3eac21e03')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
