# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.6.r70.g66556d9
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('x86_64' 'i686')
url="http://st.suckless.org"
license=('MIT')
options=(zipman)
depends=('libxft' 'libxext')
makedepends=('git' 'ncurses')
conflicts=('st')
_commit=66556d967028a0b770e9bfcb9667389a6e994a58
source=("${_pkgname}::git+http://git.suckless.org/st#commit=$_commit"
        config.h
        0001-Use-more-sane-logic-for-word-delimiters.patch)
md5sums=('SKIP'
         '90f30d8170b5e697cce0b33408b9e509'
         'd066bd7e0625a17c89325ffd87a2106b')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
