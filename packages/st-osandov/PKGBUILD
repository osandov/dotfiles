# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.7.r38.g0ac685f
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="https://st.suckless.org"
conflicts=('st')
_commit=0ac685fc015362e749bf82cfaa3cfe2dd9b305f0
source=("${_pkgname}::git+https://git.suckless.org/st#commit=$_commit"
        config.h
        0001-Whitelist-key-event-modifiers-for-shortcuts.patch
        0002-Use-more-sane-logic-for-word-delimiters.patch)
sha256sums=('SKIP'
            '782b4a2fafab08fcac44148dce48634e93799bf100bb06886d24b0d872ee880d'
            'de926eb00425f327897b495705c6dacfe7e195dd8fc7f39617c409e61a6dfa1b'
            '487512b9c394ea6bc854aff08650fb0716b085cc9864c28592543a0288e8744d')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  # skip terminfo which conflicts with ncurses
  sed -i '/^\s*tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
