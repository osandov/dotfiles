# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=st-osandov
_pkgname=st
pkgver=0.7.r26.gb331da5
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
conflicts=('st')
_commit=b331da550b290d54592b7ba11242c92f5a303a48
source=("${_pkgname}::git+http://git.suckless.org/st#commit=$_commit"
        config.h
        0001-Whitelist-key-event-modifiers-for-shortcuts.patch
        0002-Use-more-sane-logic-for-word-delimiters.patch)
sha256sums=('SKIP'
            '782b4a2fafab08fcac44148dce48634e93799bf100bb06886d24b0d872ee880d'
            '7767a226e89509bf68a4625660f8156bdf35caab9d81180126b5799353d0527d'
            'b0ee6cea185bc39b48e502d9267b7dbc82838357cb6cc143a12b32fd02af0ac4')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
