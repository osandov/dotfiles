# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r22.g3756f7f
pkgrel=1
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="https://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=3756f7f6b8dccf7a31789ffaf073a76fd1a87eb9
source=("${_pkgname}::git+https://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Add-support-for-XBM-icons-in-the-status-bar.patch)
sha256sums=('SKIP'
            '0d146ea2488c62b384c2f250a2c85f9fc89306d08c2cfaa3a05f5d49ae4fb54c'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '80b83c001c612740b365aeae512ef5a01a08981b1695eb3c88886a14acedf80a')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
