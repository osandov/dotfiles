# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r19.g6aa8e37
pkgrel=2
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="http://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=6aa8e37efe22c8a2a7713d9a437491c564c04b7e
source=("${_pkgname}::git+http://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Set-class-name-on-status-bar.patch
        0002-Add-support-for-XBM-icons-in-the-status-bar.patch)
sha256sums=('SKIP'
            'e83d4c0be152f07bf01f9823b8eb6e6b8327eb99c414bf5f1d3fa508d01df4cc'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '903927c6beb3fb08df584e8f91f7f5ff00624e7ad6a0c97127dea5942cb407f6'
            '346877975a940592db3aabdd1e8a31b7bda89b19c7c21e4ddc873667507c54f3')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
