# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.2.r0.gcb3f58a
pkgrel=1
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="https://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=cb3f58ad06993f7ef3a7d8f61468012e2b786cab
source=("${_pkgname}::git+https://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Add-support-for-XBM-icons-in-the-status-bar.patch)
sha256sums=('SKIP'
            'd36917a5a6a494e062c1958dee2e1e3b249f90bb7daf3887220f9de2a15a1aec'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '397fb87bc2d452dd706e57d04ec98d22faa9b7ee6d4b417a1df85f8e2fb22529')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
