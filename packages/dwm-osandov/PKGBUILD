# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r2.g3465bed
pkgrel=4
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="http://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=3465bed290abc62cb2e69a8096084ba6b8eb4956
source=("${_pkgname}::git+http://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Set-class-name-on-status-bar.patch
        0002-Add-control-socket-and-wordy-status-toggle.patch
        0003-Add-support-for-XBM-icons-in-the-status-bar.patch
        0004-Set-the-EWMH-_NET_SUPPORTING_WM_CHECK-atom.patch)
md5sums=('SKIP'
         '0b1c9855b4514676e0f1fc927edd1a32'
         '939f403a71b6e85261d09fc3412269ee'
         'c9c5e1ff71f0b7cfda92191ccbb82b6a'
         '793f2104b9746e28b168715ea0b52d25'
         'a9aa40b33fdb0ecc2ef689f31531eb1a'
         '50842eb3c99eeac30609c73585ac6a07')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
