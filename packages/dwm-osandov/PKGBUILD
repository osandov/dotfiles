# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r18.gceac8c9
pkgrel=1
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="http://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=ceac8c91ff3bf45ae53135658d6f560cb2335133
source=("${_pkgname}::git+http://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Set-class-name-on-status-bar.patch
        0002-Add-control-socket-and-wordy-status-toggle.patch
        0003-Add-support-for-XBM-icons-in-the-status-bar.patch)
sha256sums=('SKIP'
            'a4ba8ddbd00a55b99f1f9cfc93f44b691442380d5686bfa6920cbd8982ba4c1f'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            'c186e89ac8b8484e4ec5c619ea16820d6c43a6e2abd9856eda6ca35e821768d6'
            '6412da979fc298eb28dc5ab51f9680622f189a18ce04046f48759d31c671f3fb'
            '9bdacf69b7b894ea278a9d2dbee27cff9fb8f2ff9e1506d10a14f106112b58f5')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
