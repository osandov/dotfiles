# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r3.gcd2d754
pkgrel=1
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="http://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=cd2d7549b3ae5ec234b45d85608f79f4d3aaa851
source=("${_pkgname}::git+http://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Set-class-name-on-status-bar.patch
        0002-Add-control-socket-and-wordy-status-toggle.patch
        0003-Add-support-for-XBM-icons-in-the-status-bar.patch
        0004-Set-the-EWMH-_NET_SUPPORTING_WM_CHECK-atom.patch)
md5sums=('SKIP'
         '0b1c9855b4514676e0f1fc927edd1a32'
         '939f403a71b6e85261d09fc3412269ee'
         '0fb300244fe194f52ff177a77a7d33f1'
         'cf480d2aa524d448019c9f9d90b06623'
         '3ca5d9c1bec6c89edcd7b50b32ab8841'
         '0c6106b597b821dc1b990fed32fa3b01')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
