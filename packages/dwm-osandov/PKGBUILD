# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=dwm-osandov
_pkgname=dwm
pkgver=6.1.r14.gbb3bd6f
pkgrel=1
pkgdesc="A dynamic window manager for X"
arch=('x86_64' 'i686')
url="http://dwm.suckless.org"
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
conflicts=('dwm')
_commit=bb3bd6fec37174e8d4bb9457ca815c00609e5157
source=("${_pkgname}::git+http://git.suckless.org/dwm#commit=$_commit"
	      config.h
	      dwm.desktop
        0001-Set-class-name-on-status-bar.patch
        0002-Add-control-socket-and-wordy-status-toggle.patch
        0003-Add-support-for-XBM-icons-in-the-status-bar.patch)
sha256sums=('SKIP'
            '3feba2d61b1fa58c5a559ff6adca6e0ee86c0db0f8aa73d2c4e4877e5dd673d5'
            'bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '0d22bf70a7dc14175b68acb6a2c45bd48b3158ad711ba5edec98620c2ebb3d66'
            '991dbecbc54fcb7ea943a43dd112390f5b2490d492e75e4964a5166a794cc136'
            '7d34b42b475851fabffc9984971f07188d8cd8f1212436513c482d4c3e8a8f83')

pkgver() {
  cd "$_pkgname"
  git describe --abbrev=7 --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git apply "$srcdir"/*.patch
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$_pkgname"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
}

# vim:set ts=2 sw=2 et:
