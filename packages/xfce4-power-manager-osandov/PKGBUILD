# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=xfce4-power-manager-osandov
_pkgname=xfce4-power-manager
pkgver=1.4.4
pkgrel=1
_pkgver=1.4.4
_pkgrel=2
pkgdesc="Power manager for Xfce desktop"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'upower' 'libnotify' 'hicolor-icon-theme')
optdepends=('xfce4-panel: for the Xfce panel plugin'
            'lxpanel: for the LXDE panel plugin')
makedepends=('intltool' 'xfce4-panel' 'lxpanel')
conflicts=('xfce4-power-manager')
source=(http://archive.xfce.org/src/apps/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2
        0001-Run-screensaver-synchronously.patch)
sha256sums=('c50ec8aa7e7848c57c0f856dceb8132eb5f37585f0ac1627459ab8c882c73b07'
            '7f2ca5c8f9165f588503ead19e98b0b88fc01dd7c78447fe8a68b3562eaf548f')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  cat "$srcdir"/*.patch | patch -p1
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-network-manager \
    --enable-polkit \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
