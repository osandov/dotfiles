# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=xfce4-notifyd-osandov
_pkgname=xfce4-notifyd
pkgver=0.2.4
pkgrel=1
_pkgver=0.2.4
_pkgrel=2
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool')
provides=('notification-daemon')
conflicts=('xfce4-notifyd')
install=$_pkgname.install
source=(http://archive.xfce.org/src/apps/$_pkgname/0.2/$_pkgname-$pkgver.tar.bz2
        0001-Use-a-GTK_WINDOW_POPUP-for-notification-window.patch)
sha256sums=('8c7ed62f9496816d1391281f77d1b32216f9bf6fd22fbe4f6f3f4e07a6bbced0'
            '52cbdcbb459dd774fa58551ff78d0a9ba34b748d5b0444b870d8a79aa871c54c')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  cat "$srcdir"/*.patch | patch -p1
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
