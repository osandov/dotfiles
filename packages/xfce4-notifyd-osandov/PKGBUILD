# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=xfce4-notifyd-osandov
_pkgname=xfce4-notifyd
pkgver=0.3.6
pkgrel=1
_pkgver=0.3.6
_pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
provides=('notification-daemon')
conflicts=('xfce4-notifyd')
source=(http://archive.xfce.org/src/apps/$_pkgname/${_pkgver%.*}/$_pkgname-$_pkgver.tar.bz2
        0001-Use-a-GTK_WINDOW_POPUP-for-notification-window.patch)
sha256sums=('f4ca7c0dadd3d4cdf8cd3c8ae60ccea77b8cf409f8517161796364eb1d766cf9'
            'fd13cd3ca5243d22bb5b338c505ccfed767df635f9d4f1e7b15692c90fc968e2')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  cat "$srcdir"/*.patch | patch -p1
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
