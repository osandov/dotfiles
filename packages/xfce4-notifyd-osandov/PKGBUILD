# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=xfce4-notifyd-osandov
_pkgname=xfce4-notifyd
pkgver=0.3.2
pkgrel=1
_pkgver=0.3.2
_pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
provides=('notification-daemon')
conflicts=('xfce4-notifyd')
source=(http://archive.xfce.org/src/apps/$_pkgname/${_pkgver%.*}/$_pkgname-$_pkgver.tar.bz2
        0001-Use-a-GTK_WINDOW_POPUP-for-notification-window.patch)
sha256sums=('c554db55d9d759c32115e9e5da029bd68b07628438ef2bac7ae4b458567c85a4'
            'fa6958d4cb39a7b055174c0e4e9a610bd0285a1f057f25d0a034174b37617b13')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  cat "$srcdir"/*.patch | patch -p1
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
