# Maintainer: Omar Sandoval <osandov@osandov.com>

pkgname=xfce4-notifyd-osandov
_pkgname=xfce4-notifyd
pkgver=0.3.0
pkgrel=1
_pkgver=0.3.0
_pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
provides=('notification-daemon')
conflicts=('xfce4-notifyd')
source=(http://archive.xfce.org/src/apps/$_pkgname/${_pkgver%.*}/$_pkgname-$_pkgver.tar.bz2
        0001-Use-a-GTK_WINDOW_POPUP-for-notification-window.patch)
sha256sums=('7076081424932b40d03366a7518daf495f81eb780f376819e59e76d65d811100'
            'a00cf4fe4693acf643e5c515a30f9382a28c27fd631cd0b68cf5eeb2f069fab1')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  cat "$srcdir"/*.patch | patch -p1
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
